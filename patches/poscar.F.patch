

!=======================================================================
!
! read direct lattice from stdin
! Patch by T.Tian (alchem0x2a) for upgrading VASP's interactive mode
! The implementation should not interfere with normal VASP usage but 
! do tests at your own risk!
!
!=======================================================================
      SUBROUTINE INLATT(LATT_CUR, T_INFO, DYN, IU6, IU0, LSTOP, MYCOMM)
      USE prec
      USE lattice
      USE main_mpi
      IMPLICIT NONE

      TYPE (latt)::       LATT_CUR
      TYPE (type_info) :: T_INFO
      TYPE (dynamics)  :: DYN
      INTEGER :: IU6, IU0
      LOGICAL :: LSTOP
      TYPE (communic) :: MYCOMM
    ! local
      INTEGER NI, IERR, I, J
      REAL(q) :: LATT_A_OLD(3, 3), LATT_B_OLD(3, 3)

      ! Local copy of the old lattice parameters
      LATT_A_OLD = LATT_CUR%A
      LATT_B_OLD = LATT_CUR%B
      ! Update the lattice on I/O rank else 0
      ! Use M_sum_b to gather results
      IF (IU6>=0) THEN
         IF (IU0>=0) WRITE(IU0,'(A)') 'LATTICE: reading from stdin'
         DO NI=1,3
            READ(*,*,  IOSTAT=IERR) LATT_CUR%A(1,NI), LATT_CUR%A(2,NI), LATT_CUR%A(3,NI)
            IF (IERR/=0) EXIT
         ENDDO

         IF (IERR/=0) THEN
            LSTOP=.TRUE.
         ELSE
            LSTOP=.FALSE.
         ENDIF
         CALLMPI( M_sum_i(MYCOMM, IERR, 1))
         IF (IERR==0) THEN
            CALLMPI( M_sum_d(MYCOMM, LATT_CUR%A(1,1), 9))
         ENDIF
      ELSE
         IERR=0
         CALLMPI( M_sum_i(MYCOMM, IERR, 1))
         IF (IERR==0) THEN
            LATT_CUR%A(:,1:3)=0
            CALLMPI( M_sum_d(MYCOMM, LATT_CUR%A(1,1), 9))
         ENDIF
      ENDIF
      ! Update other lattice params (reciprocal, volume, norm vect) on all ranks
      CALL LATTIC(LATT_CUR)
      ! If current rank allows output, write the positions
      IF (IU0 >= 0) THEN
            WRITE(IU0, '(A,3X,A,19X,A)') 'New', 'direct lattice vectors', 'reciprocal lattice vectors'
            WRITE(IU0, 8900) ((LATT_CUR%A(I,J),I=1,3),(LATT_CUR%B(I,J),I=1,3),J=1,3)
            WRITE(IU0, '(A,3X,A,19X,A)') 'Old', 'direct lattice vectors', 'reciprocal lattice vectors'
            WRITE(IU0, 8900) ((LATT_A_OLD(I,J),I=1,3),(LATT_B_OLD(I,J),I=1,3),J=1,3)
            WRITE(IU0, '(A)') 'LATTICE: read from stdin'
      ENDIF
8900  FORMAT(3(2(3X,3F13.7)/)/)
      END SUBROUTINE INLATT


